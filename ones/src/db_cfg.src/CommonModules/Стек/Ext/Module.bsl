//////////////////////////////////
// Исключения

Функция ОшибкаСтекПолный() Экспорт
	Возврат "/ОшибкаСтекПолный/";	
КонецФункции

Функция ОшибкаСтекПустой() Экспорт
	Возврат "/ОшибкаСтекПустой/";	
КонецФункции

//////////////////////////////////
// API

// Фабричный метод
Функция СтекСоздать() Экспорт
	Возврат Новый Структура("Стек", Новый Массив);	
КонецФункции

// Фабричный метод
Функция СтекОграниченныйСоздать(Емкость) Экспорт
	Результат = СтекСоздать();
	Результат.Вставить("Емкость", Емкость);
	Возврат Результат;
КонецФункции

Функция Снять(СтекСтруктура) Экспорт
	Если Пустой(СтекСтруктура) Тогда
		ВызватьИсключение ОшибкаСтекПустой();
	КонецЕсли;
	Результат = СтекСтруктура.Стек[0];
	СтекСтруктура.Стек.Удалить(0);
	Возврат Результат;
КонецФункции

Функция Поместить(СтекСтруктура, Значение) Экспорт
	Если Полный(СтекСтруктура) Тогда
		ВызватьИсключение ОшибкаСтекПолный();
	КонецЕсли;
	СтекСтруктура.Стек.Вставить(0, Значение);
	Возврат Значение;
КонецФункции

Функция Пустой(СтекСтруктура) Экспорт
	Возврат СтекСтруктура.Стек.Количество() = 0;			
КонецФункции

Функция Полный(СтекСтруктура) Экспорт
	Если ЭтоОграниченныйСтек(СтекСтруктура) Тогда
		Возврат СтекСтруктура.Емкость <= СтекСтруктура.Стек.Количество();
	КонецЕсли;
	Возврат Ложь;
КонецФункции

Функция ЭтоОграниченныйСтек(СтекСтруктура) Экспорт
	Возврат СтекСтруктура.Свойство("Емкость");	
КонецФункции